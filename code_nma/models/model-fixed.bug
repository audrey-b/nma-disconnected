
#### flat prior on baseline rates for each of NUMTRIAL trials
#### 1-st agent is reference, effects for agents 2 thru NUMAGENT relative to this

model {

  ### parameter rho, as per PG's notes (Feb 2013)
  rho <- 0.5
  rho.inv <- 1/rho
  rhocomp.inv <- 1/(1-rho)

  ### parameter sigma^2, as per notes
  sig2 ~ dunif(0,10)
  sig2.inv <- 1/sig2

  ### d[i] is typical log-OR, (i+1)-st agent compared to 1st-agent, as per notes
  for (i in 1:(NUMAGENTS-1)) {
   d[i] ~ dnorm(0, 0.000001)
  }

  ### ltnt[] are trick variables to represent correlation structure
  for (i in 1:NUMTRIALS) {
    ltnt[i] ~ dnorm(0, ltnt.prc)
  }
  ltnt.prc <- rho.inv*sig2.inv

  ### alpha[i] is i-th trial baseline
  for (i in 1:NUMTRIALS) {
    alpha[i] ~ dnorm(0, 0.000001)
  }

  ### dlta[i,j-1] is log-OR for j-th agent compared to 1st, in i-th trial
  ### whether or not these arm exists in the i-th trial
  for (i in 1:NUMTRIALS) {
    logit(scsrt[i,1]) <- alpha[i]
    for (j in 2:NUMAGENTS) {
      dlta[i,j-1] ~ dnorm(dlta.mn[i,j-1], dlta.prc)
      dlta.mn[i,j-1] <- ltnt[i]+d[j-1]
      logit(scsrt[i,j]) <- alpha[i]+dlta[i,j-1]
    }
  }
  dlta.prc <- rhocomp.inv*sig2.inv

### TOTARMS = sum over all trials of number of arms in each trial
### think of each trial,arm combo being a row in the data matrix
### columns tell which *trial*, which *agent*, number treated (n)
### and number of successes (y)

 for (i in 1:TOTARMS) {
   y[i] ~ dbin( scsrt[trial[i],agent[i]], n[i]) 
                
 }

}
